plugins {
	id 'fabric-loom' version '0.4-SNAPSHOT'
    id 'java-gradle-plugin'
}

apply plugin: 'maven'

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "crowdin-translate"
version = "1.3+1.16"

minecraft {
	refmapName = "crowdin-translate-refmap.json";
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// These are, actually, not even used in this project.
// They are only included to make loom happy.
// Use old versions intentionally to make this work with a broad range of MCs.
dependencies {
    minecraft  "com.mojang:minecraft:1.15.2"
    mappings   "net.fabricmc:yarn:1.15.2+build.7:v2"
    modImplementation "net.fabricmc:fabric-loader:0.7.2+build.174"
}


// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
    manifest {
        attributes(
            'Main-Class': 'de.guntram.mcmod.crowdintranslate.CrowdinTranslate'
        )
    }
    
}

gradlePlugin {
    plugins {
        gradleplugin {
            id = 'de.guntram.mcmod.crowdin-translate'
            implementationClass = 'de.guntram.mcmod.crowdintranslate.GradlePlugin.CrowdinTranslatePlugin'
        }
    }
}

group = "de.guntram.mcmod"
uploadArchives {
    repositories {
        mavenDeployer {
	        repository(url: "file://localhost/tmp/mymavenrepo")
        }
    }
}

task publish(dependsOn: uploadArchives, type: Exec) {
    commandLine "rsync", "-av", "/tmp/mymavenrepo/", "maven@minecraft.guntram.de:/var/www/html/maven/"
}
