plugins {
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

sourceCompatibility = 16
targetCompatibility = 16

archivesBaseName = "crowdin-translate"
version = "1.4+1.18"

loom {
    mixin.defaultRefmapName = "crowdin-translate-refmap.json";
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// These are, actually, not even used in this project.
// They are only included to make loom happy.
// Use old versions intentionally to make this work with a broad range of MCs.
dependencies {
    minecraft "com.mojang:minecraft:1.18"
    mappings "net.fabricmc:yarn:1.18+build.1:v2"
    modImplementation "net.fabricmc:fabric-loader:0.12.8"
}


// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
    manifest {
        attributes(
            'Main-Class': 'de.guntram.mcmod.crowdintranslate.CrowdinTranslate'
        )
    }
}

gradlePlugin {
    plugins {
        gradleplugin {
            id = 'de.guntram.mcmod.crowdin-translate'
            implementationClass = 'de.guntram.mcmod.crowdintranslate.GradlePlugin.CrowdinTranslatePlugin'
        }
    }
}

group = "de.guntram.mcmod"
publishing {
    publications {
	mavenJava(MavenPublication) {
	    artifact(remapJar) {
		// builtBy remapJar
	    }
	}
    }
    repositories {
        maven {
            url = "file://tmp/mymavenrepo"
        }
    }
}

task mypublish(dependsOn: publish, type: Exec) {
    commandLine "rsync", "-av", "/tmp/mymavenrepo/", "maven@minecraft.guntram.de:/var/www/html/maven/"
}
